5 X=Y=L=DL=R=DR=SPEED=POS=TOT=TIME=Z=BACK
10 ROAD=5:FIELD=12:BUSH=4:CAR=1
20 DOWN=4096+174
40 GOSUB 2000
60 TOT=0:TIME=425:Y=TIME/12+3
70 POS=1500:X=POS/75:BACK=ROAD
80 L=16:DL=0:R=20:DR=0
100 COLOR=BACK: VLIN Y-1, Y AT X
150 CALL DOWN
160 Z = PEEK(-16336):Z=PEEK(-16336)
200 POS=POS+PDL (0)-128: IF POS<0 THEN POS=0: IF POS>2999 THEN POS=2999
220 TIME=TIME-1:Y=TIME/12+3
230 X=POS/75:BACK=SCRN(X,Y)
250 COLOR=CAR: VLIN Y-1,Y AT X
300 COLOR=FIELD: HLIN 0,39 AT 1: HLIN 0,39 AT 0
310 COLOR=BUSH: PLOT RND(40),1
320 COLOR=ROAD: HLIN L,L+4 AT 1: HLIN R,R+4 AT 1:HLIN L,L+4 AT 0: HLIN R,R+4 AT 0
350 Z=PEEK(-16336):Z=PEEK(-16336)
400 IF NOT RND(5) THEN DL= RND(3)-1
410 IF NOT RND(5) THEN DR= RND(3)-1
420 IF L=1 THEN DL=1: IF L=33 THEN DL=-1
430 IF R=1 THEN DR=1: IF R=33 THEN DR=-1
450 L=L+DL:R=R+DR
480 SPEED=(255-PDL(1))/5
490 FOR K=0 TO SPEED: NEXT K
500 TOT=TOT+60-SPEED
520 IF BACK=FIELD THEN 1000
540 Z=PEEK(-16336):Z=PEEK(-16336)
550 VTAB 22: TAB 9: PRINT TIME/6;" "; :TAB 22: PRINT TOT; " "; :TAB 37 : PRINT 90-SPEED
600 IF TIME>0 THEN 100
700 TEXT : CALL -936: VTAB 5
710 TAB 12: PRINT "--- GAME OVER ---"
720 VTAB 12: PRINT " YOUR SCORE OF "; TOT;" IS ";
730 IF TOT>10000 THEN 750
740 PRINT "TERRIBLE!": PRINT : IF TOT<5000 THEN PRINT "(YOU SHOULD BE ASHAMED OF YOURSELF!)"
750 IF TOT>10000 AND TOT<=15000 THEN PRINT "PRETTY BAD!"
760 IF TOT>15000 AND TOT<=19000 THEN PRINT "ABOUT AVERAGE."
770 IF TOT>19000 AND TOT<=22000 THEN PRINT "VERY GOOD!"
780 IF TOT>22000 THEN PRINT "EXCELLENT!"
790 IF TOT>24000 THEN CALL 4352
800 FOR K=1 TO 1000: NEXT K
810 VTAB 20: PRINT "PUSH EITHER PADDLE BUTTON TO PLAY AGAIN"
820 IF PEEK(-16286)<128 AND PEEK(-16287)<128 THEN 820
850 GOSUB 2050
900 GOTO 60
1000 FOR K=1 TO 20: Z=PEEK(-16336):NEXT K
1010 TOT=TOT-5*(50-SPEED):TIME=TIME-1
1020 IF TOT<0 THEN TOT=0
1050 GOTO 550
2000 TEXT: CALL -936
2010 VTAB 10: TAB 10: PRINT "*** LIT'L RED BUG ***"
2020 PRINT : TAB 19: PRINT "BY"
2030 PRINT : TAB 15: PRINT "BISH BOBHOP"
2040 FOR K=1 TO 1500: NEXT K
2045 GOSUB 3000
2050 GR: POKE -16298,0
2060 VTAB 22: PRINT " TIME:";:TAB 15:PRINT "SCORE:";:TAB 30: PRINT "SPEED:                                      " 
2070 POKE 50,63: VTAB 24: TAB 2: PRINT " *** LIT'L RED BUG ***";: POKE 50,255
2100 FOR K=0 TO 39
2110 COLOR=FIELD: HLIN 0,39 AT K
2120 COLOR=BUSH: IF K MOD 2 THEN PLOT RND(40),K
2130 COLOR=ROAD: HLIN 16-K/8,24+K/8 AT K
2140 NEXT K
2200 RETURN
3000 CALL -936: TAB 10: PRINT "*** LIT'L RED BUG ***"
3010 PRINT : PRINT : PRINT "THE OBJECT OF THIS GAME IS TO DRIVE YOUR"
3020 PRINT "LITTLE RED VOLKSWAGEN DOWN THE ROAD AS"
3030 PRINT : PRINT "FAST AS YOU CAN WITHOUT GOING OFF THE"
3040 PRINT : PRINT "PAVEMENT. GAME PADDLE 0 IS YOUR STEER-"
3050 PRINT : PRINT "ING WHEEL, AND PADDLE 1 IS YOUR ACCELER-"
3060 PRINT "ATOR. HAVE A NICE RIDE!"
3090 FOR K=1 TO 1000: NEXT K
3095 POKE 50,64*(1-CAR)-1:PRINT "[M]ONO/";: POKE 50,64*(CAR)-1:PRINT "[C]OLOR":POKE 50,255
3100 VTAB 22: PRINT "PUSH EITHER GAME PADDLE BUTTON TO BEGIN"
3110 IF PEEK (-16286)<128 AND PEEK(-16287)<128 THEN 3110
3120 IF PEEK (-16286)>127 OR PEEK (-16287)>127 THEN 3120
3200 CALL -936: RETURN
